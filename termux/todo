#!/data/data/com.termux/files/usr/bin/env fish

while true
    clear
    echo 'What to do?'
    echo
    echo '  1. Open fish'
    echo '  2. Open nnn'
    echo '  3. Synchronize files'
    echo '  4. Upload files'
    echo '  5. Download files'
    echo '  6. Update termux config'
    echo '  7. Quit'
    echo
    read --line --prompt-str 'Your choice: ' choice
    switch $choice
        case 1
            clear
            fish
        case 2
            clear
            nnn
        case 3
            clear
            bkpsync $REMOTE
            read --line --prompt-str 'Press ENTER to continue...'
        case 4
            clear
            bkpsync --upload $REMOTE
            read --line --prompt-str 'Press ENTER to continue...'
        case 5
            clear
            bkpsync --download $REMOTE
            if test -f ~/downloads/clipboard
                echo 'Update clipboard contents'
                termux-clipboard-set < ~/downloads/clipboard
                rm ~/downloads/clipboard
            end
            read --line --prompt-str 'Press ENTER to continue...'
        case 6
            clear
            pushd ~/projects/termux-config && git pull
            read --line --prompt-str 'Make? [y/N] ' answer
            if string match --quiet --regex '^[Yy]$' $answer
                make
            end
            popd
            read --line --prompt-str 'Press ENTER to continue...'
        case 7
            echo 'Exit'
            break
    end
end
