#!/data/data/com.termux/files/usr/bin/env fish

set bold (tput bold)
set reset (tput sgr0)

while set choice ( \
        gum choose --header 'Termux Menu' --header.align center --header.bold --header.width (math (tput cols) - 2) --header.border rounded \
            'Open fish' 'Open nnn' 'Open password manager' 'Synchronize files' 'Upload files' 'Download files' 'Update packages' 'Update termux config' Quit \
    )
    switch $choice
        case 'Open fish'
            clear
            fish
        case 'Open nnn'
            clear
            nnn
        case 'Open password manager'
            clear
            fpass
        case 'Synchronize files'
            clear
            time bkpsync $REMOTE
            read --prompt-str 'Press ENTER to continue...'
        case 'Upload files'
            clear
            time bkpsync --upload $REMOTE
            read --prompt-str 'Press ENTER to continue...'
        case 'Download files'
            clear
            time bkpsync --download $REMOTE
            if test -f ~/downloads/clipboard
                echo 'Update clipboard contents'
                termux-clipboard-set <~/downloads/clipboard
                rm ~/downloads/clipboard
            end
            read --prompt-str 'Press ENTER to continue...'
        case 'Update packages'
            clear
            echo $bold':: Update repositories'$reset
            pkg update || termux-change-repo
            echo $bold':: Update packages'$reset
            pkg upgrade -y
            echo $bold':: Remove old packages'$reset
            pkg autoclean
            apt autoremove -y
            echo $bold':: Update pass'$reset
            pass git pull
            read --prompt-str 'Press ENTER to continue...'
        case 'Update termux config'
            clear
            echo $bold':: Update linux-config'$reset
            pushd ~/projects/linux-config && git pull
            popd
            echo $bold':: Update termux-config'$reset
            pushd ~/projects/termux-config && git pull && make
            popd
            echo $bold':: Update pass'$reset
            pass git pull
            read --prompt-str 'Press ENTER to continue...'
        case Quit
            break
    end
    clear
end
